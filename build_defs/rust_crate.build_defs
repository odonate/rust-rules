def _rustc_crate_cmds(
        name:str,
        crate_name:str,
        edition:str="2018",
        src_root:str=None,
        err_format:str='json',
        crate_type:str|list='lib',
        emit:str|list=['dep-info', 'metadata', 'link'],
        codegen:dict={'embed-bitcode': 'no'},
        features:list=None,
        out_dir='target/debug/deps',
        search_path:str|list|dict=None,
        cap_lints='allow',
        externs:list=None,
        build_out:str=None,
        warns:list=None,
        allows:list=None,
        compile_env:dict=None,
):
    """Returns the commands to pass to rustc, and tools to use for it.
    
    We replicate the way Cargo invokes the rustc compiler as close as possible.

    Args:
        name (str): Name of the rule. Required.
        crate_name (str): Name of the crate being built. Required.
        edition (str): Edition of rust to use for this crate specified in the crate's Cargo.toml.
                  Rust has different editions (2015, 2018, 2021) that represent sets of language 
                  features. Ensures crate is compiled with appropriate features.
        src_path (str): The path to script being compiled. Specifies a path to append to the $SRCS of
                  the compilation rule using these commands.
        err_format (str): Controls the format of error messages. Messages are printed to stderr. The
                  valid options are: human, json, short.
        crate_type (str|list): Instructs rustc on which crate types to build. The valid crate types
                  are: lib (defaults to rlib), rlib, staticlib, dylib, cdylib, bin, proc-macro.
        emit (str|list): Controls the type of output files generated by the compiler. The valid emit
                  kinds are: asm, dep-info, link, llvm-bc, llvm-ir, metadata, mir, obj.
        codegen (dict): All of these options are passed to rustc via -C flags, short for codegen. To
                  see a version of this list for your compiler run `plz rustc -- -C help`.
        features (list): Configures the compilation environment. These flags turn on or off various
                  `#[cfg]` settings for conditional compilation. For example, `--cfg feature="serde"`
                   turns on `#[cfg(feature = "serde")]`.
        out_dir (str): The outputted crate will be written to this directory.
        search_path (str|list|dict): Options are passed to rust via -L flags. Adds path to search 
                  for crates and libraries. If a dict, it specifies the search_path in the form 
                  `-L KIND=PATH` where KIND may be one of: dependency, crate, native, framework, all.
        externs (list): Specifies where external libraries are located. Allows you to pass the name
                  and location for an external crate of a direct dependency. The given crate name is
                  added to the extern prelude, similar to specifying `extern crate` within the root
                  module.
        cap_lints (string): Sets the lint cap level, the maximum level for all lints. This feature
                  is used heavily by Cargo; it will pass `--cap-lints allow` when compiling your 
                  dependencies, so that if they have any warnings, they do not pollute the output.
        build_out (string): The output of any build scripts that were run.
        warns (list): Sets which lints should be set to warn level. Note: The order of these lint
                  level arguments is taken into account, see 
                  `https://doc.rust-lang.org/rustc/lints/levels.html#via-compiler-flag` for more
                  information.
        allows (list): Sets which lints should be set to allow level. Note: The order of these lint
                  level arguments is taken into account, see 
                  `https://doc.rust-lang.org/rustc/lints/levels.html#via-compiler-flag` for more
                  information.
    """
    export_envs = [f'export {k}={v} && ' for k,v in sorted(compile_env.items())] if compile_env else []
    cmd = export_envs + ['$TOOLS_RUSTC']
    if crate_name:
        cmd.append('--crate-name ' + crate_name)
    if edition:
        cmd.append(f'--edition={edition}')
    if src_root:
        cmd.append(f'$SRCS/{src_root}')
    else:
        cmd.append('$SRCS')
    if err_format:
        cmd.append(f'--error-format={err_format}')
        if err_format == 'json':
            cmd.append('--json=diagnostic-rendered-ansi,artifacts,future-incompat')
    cmd.append('--diagnostic-width=150')
    if crate_type is None:
        pass
    elif isinstance(crate_type, str):
        cmd.append(f'--crate-type {crate_type}')
    elif isinstance(crate_type, list):
        crate_types = ','.join(crate_type)
        cmd.append(f'--crate-type {crate_types}')
    if emit is None:
        pass
    elif isinstance(emit, str):
        cmd.append(f'--emit={emit}')
    elif isinstance(emit, list):
        emits = ','.join(emit)
        cmd.append(f'--emit={emits}')
    if codegen:
        # TODO: -C metadata=xxx-hash-xxx
        # TODO: -C extra-filename=-xxx-hash-xxx 
        cmd.extend([f'-C {k}={v}' for k,v in sorted(codegen.items())])
    if features:
        cmd.extend([f'--cfg feature=\\"{feature}\\"' for feature in features])
    if warns:
        cmd.extend([f'--warn="{warn}"' for warn in warns])
    if allows:
        cmd.extend([f'--allow="{allow}"' for allow in allows])
    if out_dir:
        # TODO: use name-metadata instead of just name.
        cmd.append(f'--out-dir {out_dir}')
    if search_path is None:
        pass
    elif isinstance(search_path, str):
        cmd.append(f'-L {search_path}')
    elif isinstance(search_path, list):
        cmd.extend([f'-L {path}' for path in search_path])
    elif isinstance(search_path, dict):
        cmd.extend([f'-L {k}={v}' for k,v in sorted(search_path.items())])
    cmd.append(f'-L $(cat $TOOLS_REPO_ROOT)/plz-out/gen/third_party/rust/{out_dir}')
    if externs:
        cmd.extend([extern for extern in externs])
    if cap_lints:
        cmd.append(f'--cap-lints {cap_lints}')
    if CONFIG.RUST.STDLIB:
        cmd.append('-L $TOOLS_STDLIB')
    if build_out:
        cmd.append(f'$(cat $(location {build_out}))') 
    cmd = ' '.join(cmd)
    return {
        'dbg': cmd + ' -g',
        'opt': cmd + ' -O',
    }, {
        'rustc': [CONFIG.RUST.RUSTC],
        'stdlib': [CONFIG.RUST.STDLIB],
        'repo_root': [CONFIG.RUST.REPO_ROOT],
    }


def rust_crate(
        # Crate params.
        name:str, crate:str, version:str, edition:str='2015', src_root:str='src/lib.rs',
        features:list=None, deps:list|dict=None, crate_type:str='lib', out_dir:str='target/debug/deps',
        warns:list=None, allows:list=None, compile_env:dict=None,     
        # Build params.
        build_root:str=None, build_deps:list|dict=None, build_out_dir:str='target/debug/build',
        build_compile_env:dict={}, build_run_env:dict={},
        os:str=None,
        arch:str=None,
        visibility:list=["PUBLIC"],
):
    base = f'{crate}-{version}'
    # Download the crate.
    url = f'https://crates.io/api/v1/crates/{crate}/{version}/download'
    download_rule = remote_file(
        name=f'{base}_download',
        url=url,
        out=f'{base}_download',
    )
    # Extract the tarball .cargo file.
    extract_rule = build_rule(
        name=base,
        tag="extraction",
        srcs=[download_rule],
        outs = [f'{base}'],
        cmd = ' && '.join([
            'tar -xvzf $SRCS',
        ]),
        building_description = f'Extracting {base}...',
    )
    extra_deps = []
    crate_fg = filegroup(
        name = base,
        tag = 'crate',
        srcs = [extract_rule],
        deps = [extract_rule],
    )
    extra_deps.append(crate_fg)
    
    # Run any required build scripts.
    if build_root:
        if not os:
            if CONFIG.OS == 'linux':
                os = 'unknown-linux-gnu'
            elif CONFIG.OS == 'darwin':
                os = 'apple-darwin'
            else:
                raise ParseError('Unknown OS %s, you must pass os explicitly to rust_crate' % CONFIG.OS)
        if not arch:
            if CONFIG.ARCH == 'amd64':
                arch = 'x86_64'
            elif CONFIG.ARCH == 'x86':
                arch = 'i686'
            else:
                raise ParseError('Unknown architecture %s, you must pass arch explicitly to rust_crate' % CONFIG.ARCH)
        host = f'{arch}-{os}'
        formatted_base = base.replace('-', '_').replace('.', '_')
        build_out_dir = f'{build_out_dir}/{formatted_base}'
        build_out = _collect_build_script_output(base, crate_fg, build_root, build_deps, edition, build_out_dir, features, host, build_compile_env, build_run_env)
        extra_deps.append(build_out)
    else:
        build_out = None

    return _compile_rust_crate(
        name=name,
        crate_fg=crate_fg,
        crate=crate,
        version=version,
        edition=edition,
        src_root=src_root,
        features=features,
        deps=deps,
        crate_type=crate_type,
        out_dir=out_dir,
        build_out=build_out,
        extra_deps=extra_deps,
        warns=warns,
        allows=allows,
        compile_env=compile_env,
        visibility=visibility,
    )

def _compile_rust_crate(
        name:str, crate_fg:str, crate:str, version:str, edition:str, src_root:str, features:list,
        deps:list|dict, crate_type:str, out_dir:str, build_out:str, extra_deps:list, warns:list,
        allows:list, compile_env:dict, visibility:list,
):
    pkg = package_name()
    base = f'{crate}-{version}'
    cmd, tools = _rustc_crate_cmds(
        name=base,
        crate_name=name,
        edition=edition,
        src_root=src_root,
        err_format='human',
        crate_type=crate_type,
        features=features,
        out_dir=out_dir,
        build_out=build_out,
        warns=warns,
        allows=allows,
        compile_env=compile_env,
    )
    if deps is None:
        pass
    elif isinstance(deps, list):
        crate_deps = deps
    elif isinstance(deps, dict):
        crate_deps = deps.values()
    else:
        fail(f'Unknown deps type {deps}')
    return build_rule(
        name = name,
        srcs = [crate_fg],
        outs = {
            'crate': [f'{out_dir}/lib{name}.rlib' if crate_type in ['lib', 'rlib'] else None],
            'so': [f'{out_dir}/lib{name}.so' if crate_type in ['proc-macro'] else None],
            'md': [f'{out_dir}/lib{name}.rmeta'],
            'dep_info': [f'{out_dir}/{name}.d'],
        },
        cmd = cmd,
        deps = crate_deps + extra_deps if deps else extra_deps,
        visibility = visibility,
        requires = ['rust'],
        labels=[
            f'rust_crate_path:{crate}:{pkg}/{out_dir}/lib{name}.rlib',
            f'rust_md_path:{crate}:{pkg}/{out_dir}/lib{name}.rmeta',
        ],
        tools = tools,
        pre_build=_collect_extern_paths(
            base=base,
            crate_name=name,
            edition=edition,
            src_root=src_root,
            crate_type=crate_type,
            features=features,
            out_dir=out_dir,
            deps=deps,
            build_out=build_out,
            warns=warns,
            allows=allows,
            compile_env=compile_env,
        ) if deps else None,
        needs_transitive_deps = True,
        building_description = f'Compiling crate for {base}...',
    )
    
def _compile_build_script(
        name:str, crate_fg:str, root:str, deps:list, edition:str, out_dir:str, features:list=None, compile_env:dict={},
):
    """Compiles the build script before execution"""
    crate_name = f'build_script_build'
    cmd, tools = _rustc_crate_cmds(
        name=name,
        crate_name=crate_name,
        edition=edition,
        src_root=root,
        err_format='human',
        crate_type='bin',
        emit=['dep-info', 'link'], # Omits metadata.
        features=features,
        out_dir=out_dir,
    )
    return build_rule(
        name = name,
        tag = 'compile_build_script',
        srcs = [crate_fg],
        outs = {
            'build_bin': [f'{out_dir}/{crate_name}'],
            'dep_info': [f'{out_dir}/{crate_name}.d']
        },
        cmd = cmd,
        deps = deps,
        requires = ['rust'],
        tools = tools,
        env = compile_env,
        pre_build=_collect_extern_paths(
            base=name, # not required?
            crate_name=crate_name,
            edition=edition,
            src_root=root,
            crate_type='bin',
            out_dir=out_dir,
            deps=deps,
            features=features,
        ) if deps else None,
        building_description = f'Compiling build script for {name}...',
    ), tools


def _run_build_script(
        name:str, crate_fg:str, root:str, deps:list, edition:str, out_dir:str, features:list=None, host:str, compile_env:dict={}, run_env:dict={}
):
    """Run compiled build script before building package."""
    compilation, tools = _compile_build_script(name, crate_fg, root, deps, edition, out_dir, features, compile_env)

    run_env['RUST_BACKTRACE'] = 'full'
    return build_rule(
        name=name,
        tag = 'run_build_script',
        srcs = [compilation + '|build_bin'],
        cmd = ' && '.join([
            f'mkdir -p {out_dir}/out',
            # Export env vars used inside build script.
            f'export OUT_DIR={out_dir}/out',
            f'export RUSTC=$TOOLS_RUSTC',
            f'export HOST={host}',
            f'$SRCS > {out_dir}/out/output',
        ]),
        outs = { 'output': [f'{out_dir}/out'] },
        deps = [compilation],
        pass_env = ['TOOLS_RUSTC'],
        env = run_env,
        tools = tools,
        building_description = f'Running build script {name}...',
    )


def _collect_build_script_output(
        name:str, crate_fg:str, root:str, deps:list, edition:str, out_dir:str, features:list=None, host:str, compile_env:dict={}, run_env:dict={}
):
    build_exec = _run_build_script(name, crate_fg, root, deps, edition, out_dir, features, host, compile_env, run_env)
    cmd = """
    output=""
    while IFS= read -r line; do
        if [[ $line == cargo:rustc-cfg=* ]]; then
            cfg="${line#cargo:rustc-cfg=}"
            output="$output --cfg $cfg"
        fi
    done < $SRCS/output"""
    return build_rule(
        name=name,
        tag='collect_build_output',
        srcs=[build_exec + '|output'],
        cmd = ' && '.join([
            cmd,
            'echo $output > $OUTS',
        ]),
        outs = {
            'formatted_output': [f'{out_dir}/out/formatted_output'],
        },
        deps = [build_exec],
        building_description = f'Collecting build script outputs for {name}...',
    )
        
    
    
def _collect_extern_paths(
        base:str,
        crate_name:str,
        edition:str,
        src_root:str,
        crate_type:str,
        features:list,
        out_dir:str,
        deps:list|dict,
        build_out:str=None,
        warns:list=None,
        allows:list=None,
        compile_env:dict=None,
):
    """Returns the pre-build function to apply --extern to the rustc command"""
    def extern_flags(dep, labels, rename=None):
        externs = []
        for label in labels:
            flag = label.split(':')
            crate = flag[0].replace('-', '_')
            path = flag[1]
            if crate == dep[1:]:
                if rename:
                    externs.append(f'--extern {rename}={path}')
                else:
                    externs.append(f'--extern {crate}={path}')
        return externs
            
    def collect_extern_paths(name):
        externs = []
        label_format = 'rust_md_path:'
        if crate_type in ["bin"]:
            label_format = 'rust_crate_path:'
        if isinstance(deps, dict):
            for rename, dep in deps.items():
                labels = get_labels(dep, label_format)
                externs += extern_flags(dep, labels, rename)
        elif isinstance(deps, list):
            for dep in deps:
                labels = get_labels(dep, label_format)
                externs += extern_flags(dep, labels)
        else:
            fail(f'Unknown dep type {deps}')
        if externs:
            cmds, _ = _rustc_crate_cmds(
                name=base,
                crate_name=crate_name,
                edition=edition,
                src_root=src_root,
                err_format='human',
                crate_type=crate_type,
                features=features,
                out_dir=out_dir,
                build_out=build_out,
                warns=warns,
                allows=allows,
                compile_env=compile_env,
                externs=externs,
            )
            for k, v in cmds.items():
                set_command(name, k, v)
    return collect_extern_paths
